apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'checkstyle'

android {
    compileSdkVersion 30
    buildToolsVersion '29.0.3'


    defaultConfig {
        applicationId "org.schabi.newpipe"
        resValue "string", "app_name", "NewPipe"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 957
        versionName "19.9 Isayso Fork 11/22/20"
        multiDexEnabled true  //Isayso
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            multiDexEnabled true
            debuggable true

            // suffix the app id and the app name with git branch name
            def workingBranch = getGitWorkingBranch()
            def normalizedWorkingBranch = workingBranch.replaceAll("[^A-Za-z]+", "").toLowerCase()
            if (normalizedWorkingBranch.isEmpty() || workingBranch == "master" || workingBranch == "dev") {
                // default values when branch name could not be determined or is master or dev
                applicationIdSuffix ".debug"
                resValue "string", "app_name", "NewPipe Debug"
            } else {
                applicationIdSuffix ".debug." + normalizedWorkingBranch
                resValue "string", "app_name", "NewPipe " + workingBranch
                archivesBaseName = 'NewPipe_' + normalizedWorkingBranch
            }
        }

        // Keep the release build type at the end of the list to override 'archivesBaseName' of
        // debug build. This seems to be a Gradle bug, therefore
        release {
            minifyEnabled true
            shrinkResources false // disabled to fix F-Droid's reproducible build
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            archivesBaseName = 'app'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        encoding 'utf-8'
    }

    // Required and used only by groupie
    androidExtensions {
        experimental = true
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

/*ext {
    icepickVersion = '3.2.0'
    checkstyleVersion = '8.32'
    stethoVersion = '1.5.1'
    leakCanaryVersion = '2.2'
    exoPlayerVersion = '2.11.6'
    androidxLifecycleVersion = '2.2.0'
    androidxRoomVersion = '2.2.5'
    groupieVersion = '2.8.0'
    markwonVersion = '4.3.1'
}*/

ext {
    icepickVersion = '3.2.0'
    checkstyleVersion = '8.36.2'
    stethoVersion = '1.5.1'
    leakCanaryVersion = '2.2'
    exoPlayerVersion = '2.11.8'
    androidxLifecycleVersion = '2.2.0'
    androidxRoomVersion = '2.2.5'
    groupieVersion = '2.8.0'
    markwonVersion = '4.3.1'
    googleAutoServiceVersion = '1.0-rc7'
}

configurations {
    checkstyle
    //ktlint
}

checkstyle {
    configFile rootProject.file('checkstyle.xml')
    ignoreFailures false
    showViolations true
    toolVersion = checkstyleVersion
}

task runCheckstyle(type: Checkstyle) {
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'
    exclude 'main/java/us/shandian/giga/**'

    classpath = configurations.checkstyle

    showViolations true

    reports {
        xml.enabled true
        html.enabled true
    }
}

//task runKtlint(type: JavaExec) {
//    main = "com.pinterest.ktlint.Main"
//    classpath = configurations.ktlint
//    args "src/**/*.kt"
//}

//task formatKtlint(type: JavaExec) {
//    main = "com.pinterest.ktlint.Main"
//    classpath = configurations.ktlint
//    args "-F", "src/**/*.kt"
//}

afterEvaluate {
    preDebugBuild.dependsOn runCheckstyle//, runKtlint
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.1'

    //Isayso false warning:
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.4.10"

    implementation "frankiesardo:icepick:${icepickVersion}"
    kapt "frankiesardo:icepick-processor:${icepickVersion}"

    checkstyle 'com.puppycrawl.tools:checkstyle:8.37'
//    ktlint 'com.pinterest:ktlint:0.39.0'

    debugImplementation "com.facebook.stetho:stetho:${stethoVersion}"
    debugImplementation "com.facebook.stetho:stetho-okhttp3:${stethoVersion}"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"
    implementation "com.squareup.leakcanary:leakcanary-object-watcher-android:2.5"

    debugImplementation "androidx.multidex:multidex:2.0.1"

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.mockito:mockito-core:3.3.3'

    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.room:room-testing:${androidxRoomVersion}"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0', {
        exclude module: 'support-annotations'
    }
    //Version 958
    implementation 'com.github.TeamNewPipe:NewPipeExtractor:650f0920fea535e08728d895d7b21f19c740817c'

    //Version code 957
   // implementation 'com.github.TeamNewPipe:NewPipeExtractor:6701b0fe718f6bdc385221341fa473e8aaab560e'

//Version code 956
//    implementation 'com.github.TeamNewPipe:NewPipeExtractor:19c0e8700db3af35e5dfc58e4eea50e75d7c4c61'

  //  implementation 'com.github.TeamNewPipe:NewPipeExtractor:a8665fd97f223f41190fa04569af723be9e03008'
    implementation "com.github.TeamNewPipe:nanojson:1d9e1aea9049fc9f85e68b43ba39fe7be1c1f751"

    implementation "org.jsoup:jsoup:1.13.1"

    implementation 'com.squareup.okhttp3:okhttp:4.9.0'

    implementation "com.google.android.exoplayer:exoplayer:${exoPlayerVersion}"
    implementation "com.google.android.exoplayer:extension-mediasession:${exoPlayerVersion}"

    implementation "com.google.android.material:material:1.2.1"

    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"

    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${androidxLifecycleVersion}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${androidxLifecycleVersion}"
    implementation "androidx.lifecycle:lifecycle-extensions:${androidxLifecycleVersion}"

    implementation "androidx.room:room-runtime:${androidxRoomVersion}"
    implementation "androidx.room:room-rxjava2:${androidxRoomVersion}"
    kapt "androidx.room:room-compiler:${androidxRoomVersion}"

    implementation "com.xwray:groupie:${groupieVersion}"
    implementation "com.xwray:groupie-kotlin-android-extensions:${groupieVersion}"

    implementation "de.hdodenhof:circleimageview:3.1.0"
    implementation "com.nostra13.universalimageloader:universal-image-loader:1.9.5"

    implementation "io.noties.markwon:core:4.6.0"
    implementation "io.noties.markwon:linkify:4.6.0"

    implementation "com.nononsenseapps:filepicker:4.2.1"

    implementation "ch.acra:acra-core:5.5.0"

    implementation "io.reactivex.rxjava2:rxjava:2.2.19"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "com.jakewharton.rxbinding2:rxbinding:2.2.0"

    implementation "org.ocpsoft.prettytime:prettytime:4.0.6.Final"
}

static String getGitWorkingBranch() {
    try {
        def gitProcess = "git rev-parse --abbrev-ref HEAD".execute()
        gitProcess.waitFor()
        if (gitProcess.exitValue() == 0) {
            return gitProcess.text.trim()
        } else {
            // not a git repository
            return ""
        }
    } catch (IOException ignored) {
        // git was not found
        return ""
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}